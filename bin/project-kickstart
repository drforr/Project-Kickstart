#!/usr/bin/perl

use strict;
use warnings;
use Config::Any;
use Project::Kickstart;
use Project::Kickstart::L10n;

our $VERSION = 0.01;

my $lh = Project::Kickstart::L10n->get_handle( 'en_us' ) ||
  die "No language specified!\n";
my $pk = Project::Kickstart->new;
$pk->lh( $lh );

my %CONFIG;

sub debug_options {
  for ( keys %CONFIG ) {
    print "$_: $CONFIG{$_}\n";
  }
}

$CONFIG{author} ||= $ENV{KICKSTART_AUTHOR};
$CONFIG{email} ||= $ENV{KICKSTART_EMAIL};

my @config_filepaths = ( $ENV{HOME}.'/.kickstartrc.ini' );
my $cfg = Config::Any->load_files( {
  files => \@config_filepaths,
  use_ext => 1
} );
for my $config_file ( @$cfg ) {
  my ( $filename, $config ) = %$config_file;
  for my $k ( keys %$config ) {
    $CONFIG{$k} ||= $config->{$k};
  }
}

# Eat common options until we come to something without the leading dash.
# This will modify @ARGV, just like Getopt::Long.
#
while ( $ARGV[0] =~ /^-/ ) {
  my $option = shift @ARGV;
  if ( $option eq '-h' or $option eq '--help' ) {
    $pk->Usage;
  }
  elsif ( $option eq '-v' or $option eq '--version' ) {
    $pk->Usage( $lh->maketext( "Version: [_1]", $VERSION ) );
  }

  if ( $option eq '--verbose' ) {
    $CONFIG{verbose} = 1;
  }
  elsif ( $option eq '-q' or $option eq '--quiet' ) {
    $CONFIG{quiet} = 1;
  }
}

# Done with generic options, now get the mode to operate in.
#
$pk->Usage unless @ARGV;

my $mode = shift @ARGV;
$pk->config(\%CONFIG);

my %mode_map = (
  add => sub { $pk->add( \@ARGV ) },
  create => sub { $pk->create( \@ARGV ) },
  delete => sub { $pk->delete( \@ARGV ) },
  init => sub { $pk->init( \@ARGV ) },
  'rebuild-l10n' => sub { $pk->rebuild_l10n( \@ARGV ) },
);
$mode_map{rm} = $mode_map{delete};

if( exists $mode_map{$mode} ) {
  $mode_map{$mode}->();
}
else {
  $pk->Usage( $lh->maketext( "Unknown mode '[_1]'", $mode ) );
}

debug_options;

my $pks = Project::Kickstart->new;
$pks->init(\%CONFIG);
